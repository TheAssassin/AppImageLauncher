# $Id$
# Maintainer: James Kittsmiller (AJSlye) <james@nulogicsystems.com>
#
# upstream URL: https://github.com/netrunner-rolling/PKGBUILD-ds9-extras/blob/master/appimagelauncher/PKGBUILD

_pkgname=AppImageLauncher
pkgname=appimagelauncher
pkgver=1alpha+git20180413.08ff6b8
pkgrel=1
pkgdesc="A Helper application for running and integrating AppImages."
arch=('x86_64')
url="https://github.com/TheAssassin/AppImageLauncher"
license=('MIT')
depends=('qt5-base' 'fuse' 'cairo' 'binutils' 'desktop-file-utils' 'shared-mime-info' 'inotify-tools' 'libarchive')
makedepends=('git' 'cmake' 'wget' 'vim')
source=("$_pkgname::git+https://github.com/TheAssassin/$_pkgname")
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_pkgname"

  printf "1alpha+git`date +%Y%m%d -u -d "$(git show -s --format=%ci $(git rev-parse HEAD))"`.%s" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "$srcdir/$_pkgname"

  git submodule update --init --recursive
}

build() {
  cd "$srcdir/$_pkgname"

  cmake . -DCMAKE_INSTALL_PREFIX:PATH=/usr/ -DCMAKE_INSTALL_LIBDIR=/usr/lib

  make
}

package() {
  cd "$srcdir/$_pkgname"

  make DESTDIR="$pkgdir" install

  # install license files
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
